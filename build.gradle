buildscript {
    ext.kotlinVersion = '1.8.22'
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

repositories {
    google()
    mavenCentral()
}

android {
    namespace 'com.android.deskclock'
    compileSdk 35

    defaultConfig {
        applicationId 'com.android.deskclock.new'
        minSdk 23
        targetSdk 30
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        buildConfig true
    }

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDirs += "src"
            resources.srcDirs += "src"
            res.srcDirs += "res"
            assets.srcDirs += "assets"
        }
        androidTest {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDirs += 'tests/src'
            resources.srcDirs += "tests/src"
            res.srcDirs += "res"
            assets.srcDirs += "assets"
        }
    }

    signingConfigs {
        debug {
            storeFile file("debug.jks")
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            // signing with debug keystore
            signingConfig android.signingConfigs.debug
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.appcompat:appcompat:1.6.0"
    implementation "androidx.fragment:fragment-ktx:1.5.5"
    implementation "com.google.android.material:material:1.6.1"
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation "androidx.gridlayout:gridlayout:1.1.0"
    implementation "androidx.percentlayout:percentlayout:1.0.0"

    androidTestImplementation project(":")
    androidTestImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test:runner:1.7.0"
    androidTestImplementation "androidx.test:rules:1.7.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.7.0"
}
